//======================================================================
// color_bounce.a65
// ----------------
// Transition from fade out to main. A bouncing color bar.
// Strategy: One IRQ at the extreme bottom and then one IRQ that
// moves up and down.
//
//
// 0x120 is invisible.
// 0x137 is the largest raster line.
//
//
// (c) 2017 Joachim Str√∂mbergson
//======================================================================

//------------------------------------------------------------------
// Defines.
//------------------------------------------------------------------
.const top_line    = $a8
.const bottom_line = $f0
.const end_line    = $40
.const spritedef   = $1000


//------------------------------------------------------------------
// Basic start code.
//------------------------------------------------------------------
.pc = $0801     "Basic uppstart"
	        :BasicUpstart(start)


//------------------------------------------------------------------
//------------------------------------------------------------------
* = $2000     "Effect driver code."
start:
                sei
                jsr clear_screen
                jsr init_raster_irq
                jsr init_sprite
                cli
forever:
                jmp forever

//------------------------------------------------------------------
clear_screen:
                ldy #$00
                lda #$20
cl1:
                sta $0400,y
                sta $0500,y
                sta $0600,y
                sta $0700,y
                iny
                bne cl1
                rts

//------------------------------------------------------------------
// init_raster_irq
// Set up raster interrupt at the beginning of the screen since
// we may update chars all over the screen.
//------------------------------------------------------------------
init_raster_irq:
                lda #$7f
                sta $dc0d
                sta $dd0d

                lda #$01
                sta $d01a

                lda #$10
                sta $d012
                lda #$9b
                sta $d011

                lda #<bottom_rirq
                sta $0314
                lda #>bottom_rirq
                sta $0315

                lda $dc0d
                lda $dd0d
                asl $d019
                rts

//------------------------------------------------------------------
//------------------------------------------------------------------
init_sprite:
                lda #$20
                sta $d000
                lda #$60
                sta $d001
                lda #$10
                sta $07f8
                lda #$01
                sta $d017
                lda #$01
                sta $d015

                ldy #$00
                lda #$ff
spr1:
                sta spritedef, y
                iny
                cpy #$40
                bne spr1
                rts

//------------------------------------------------------------------
// bottom_irq sets background to black and plays music.
//------------------------------------------------------------------
bottom_rirq:
                asl $d019
                lda #$00
                sta $d020
                sta $d021

                lda #<top_rirq
                sta $0314
                lda #>top_rirq
                sta $0315

                lda #$60
                sta $d012
                lda #$1b
                sta $d011
                jmp $ea31

//------------------------------------------------------------------
// Top line irq.
//------------------------------------------------------------------
top_rirq:
                asl $d019
                ldy #$00
                ldx #$00
                lda color_bar, x
                tax
tl1:
                lda $d012
tw2:
                cmp $d012
                beq tw2
                inc $02
                stx $d020
                stx $d021
                iny
                cpy #$10
                beq tdone
                tya
                tax
                lda color_bar, x
                tax
                jmp tl1
tdone:
                lda #$0e
                sta $d020
                lda #$06
                sta $d021

                lda #$28
                sta $d012
                lda #$9b
                sta $d011

                lda #<bottom_rirq
                sta $0314
                lda #>bottom_rirq
                sta $0315
                jmp $ea31

//------------------------------------------------------------------
//------------------------------------------------------------------

color_bar:
.byte $0b, $0c, $02, $04, $07, $0f, $01, $01
.byte $0f, $07, $04, $02, $0c, $0b, $00

ctr:
.byte $00

bar_start:
.byte $41

//======================================================================
// color_bounce.a65
//======================================================================
