//======================================================================
// fade_in.a65
// -----------
// Code for doing fade in effect.
//
//
// (c) 2017 Joachim StrÃ¶mbergson
//======================================================================


//------------------------------------------------------------------
// Basic start code.
//------------------------------------------------------------------
.pc = $0801     "Basic uppstart"
	        :BasicUpstart(start)


//------------------------------------------------------------------
//------------------------------------------------------------------
* = $1000     "Folding char fade out"
start:
                sei
                jsr copy_rom        

                // Switch char def bank.
                lda $d018
                ora #$0e
                sta $d018

                jsr init_raster_irq
                cli
                rts

//------------------------------------------------------------------
//------------------------------------------------------------------
 copy_rom:
                ldx #$08    
                lda #$33    
                sta $01     

                lda #$d0    
                sta $f9     
                lda #$38    
                sta $fb     
                ldy #$00    
                sty $f8             
                sty $fa     

copy_loop:      lda ($f8),y 
                sta ($fa),y 
                iny         
                bne copy_loop    
                inc $f9     
                inc $fb     
                dex         
                bne copy_loop    
                lda #$37    
                sta $01     
                rts         


//------------------------------------------------------------------
//------------------------------------------------------------------
init_raster_irq:
                sei
                lda #$7f
                sta $dc0d
                sta $dd0d

                lda #$01
                sta $d01a

                lda #$1b
                sta $d011

                lda #$a0
                sta $d012

                lda #<raster_irq
                sta $0314
                lda #>raster_irq
                sta $0315

                lda $dc0d
                lda $dd0d
                asl $d019
                cli
                rts

//------------------------------------------------------------------
//------------------------------------------------------------------
raster_irq:
                asl $d019

                dec $d020
                lda fold_chars_done
                bne fold_done
                jsr manipulate_chars
fold_done:
                inc $d020
                jmp $ea31


//------------------------------------------------------------------
//------------------------------------------------------------------
manipulate_chars:
                lda delay_ctr
                beq mc1
                dec delay_ctr
                rts
mc1:
                lda delay
                sta delay_ctr

                lda char_rows
                beq mc2
                jsr fold_16_chars
                rts
mc2:
                lda #$04
                sta char_rows
                dec char_index
                bne mc3
                inc fold_chars_done
mc3:
                rts

//------------------------------------------------------------------
//------------------------------------------------------------------
fold_16_chars:
                lda char_index
                sta curr_char
                ldx #$08
f16_loop:
                jsr fold_curr_char
                lda curr_char
                clc
                adc #$08
                sta curr_char
                dex
                bne f16_loop
                dec char_rows
                rts

//------------------------------------------------------------------
// fold_curr_char
// Will fold one row of the char given by curr_char.
//------------------------------------------------------------------
fold_curr_char:
                lda curr_char
                asl
                asl
                asl
                sta $f8
                lda curr_char
                lsr
                lsr
                lsr
                lsr
                lsr
                clc
                adc #$38
                sta $f9

                ldy #$02
                lda ($f8), y
                iny
                sta ($f8), y
                dey
                dey

                lda ($f8), y
                iny
                sta ($f8), y
                dey
                dey

                lda ($f8), y
                iny
                sta ($f8), y
                lda #$00
                dey
                sta ($f8), y

                ldy #$05
                lda ($f8), y
                dey
                sta ($f8), y
                iny
                iny

                lda ($f8), y
                dey
                sta ($f8), y
                iny
                iny

                lda ($f8), y
                dey
                sta ($f8), y
                iny
                lda #$00
                sta ($f8), y
                rts

//------------------------------------------------------------------
//------------------------------------------------------------------
delay:
.byte $03

delay_ctr:
.byte $00

char_rows:
.byte $04

char_index:
.byte $20

curr_char:
.byte $00

fold_chars_done:
.byte $00

//======================================================================
// prng.a65
//======================================================================
